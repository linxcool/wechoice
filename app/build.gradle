apply plugin: 'com.android.application'

// support for butterknife (next see dependencies in this file)
apply plugin: 'com.jakewharton.butterknife'

def keystoreFilepath = ''
def keystorePWD = ''
def keystoreAlias = ''
def keystoreAliasPWD = ''
def keyfile = file('s.keystore.temp')

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
keystoreFilepath = properties.getProperty("keystore.path")

if (keystoreFilepath) {
    keystorePWD = properties.getProperty("keystore.password")
    keystoreAlias = properties.getProperty("keystore.alias")
    keystoreAliasPWD = properties.getProperty("keystore.alias_password")
    keyfile = file(keystoreFilepath)
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.linxcool.wechoice"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            storeFile keyfile
            storePassword keystorePWD
            keyAlias keystoreAlias
            keyPassword keystoreAliasPWD
        }
        release {
            storeFile keyfile
            storePassword keystorePWD
            keyAlias keystoreAlias
            keyPassword keystoreAliasPWD
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //签名文件存在，则签名
            if (keyfile.exists()) {
                println("WITH -> buildTypes -> release: using opensdk key")
                signingConfig signingConfigs.release
            }else {
                println("WITH -> buildTypes -> release: using default key")
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    testCompile 'junit:junit:4.12'
    compile project(':library')
    compile project(':trdsdks')
    // andbase required
    // compile 'com.github.linxcool:andbase:1.0.5'
    // butterknife required
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    compile('com.jakewharton:butterknife:8.5.1', {
        // the next 2 line fix error for import appcompat-v7
        exclude module: 'support-v4'
        exclude group: 'com.android.support'
    })
    // glide required(dependence support-v4)
    compile 'com.github.bumptech.glide:glide:3.7.0'
    // xrecyclerview required
    compile 'com.jcodecraeer:xrecyclerview:1.3.2'
    // jcvideoplayer required
    compile 'fm.jiecao:jiecaovideoplayer:5.5.2'
    // test module required
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    // third libs required: wechat
    compile 'com.tencent.mm.opensdk:wechat-sdk-android-with-mta:+'
}
